buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        maven { url 'https://maven.parchmentmc.org' }
        maven { url 'https://repo.spongepowered.org/maven'}
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = 'net.joseph.vaultfilters'
archivesBaseName = 'vaultfilters'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '1.18.1-2022.03.06-1.18.2'

    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            arg "-mixin.config=mixins.vaultfilters.core.json"
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'vaultfilters'

            mods {
                vaultfilters {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'vaultfilters'

            mods {
                vaultfilters {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'vaultfilters'

            mods {
                vaultfilters {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'vaultfilters', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                vaultfilters {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
    config "mixins.${mod_id}.ae2.json"
    config "mixins.${mod_id}.rs.json"
    config "mixins.${mod_id}.sophisticatedcore.json"
    config "mixins.${mod_id}.modularrouters.json"
    config "mixins.${mod_id}.tomsstorage.json"
    config "mixins.${mod_id}.core.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'tterrag maven'
        url = 'https://maven.tterrag.com/'
    }

    maven { url "https://www.cursemaven.com" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.2.9'
    implementation fg.deobf("curse.maven:vault-hunters-official-mod-458203:5601579")

    implementation fg.deobf("com.simibubi.create:create-${create_minecraft_version}:${create_version}:slim") { transitive = false }
    implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}")
    implementation fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")

    implementation fg.deobf("curse.maven:curios-309927:4418032")
    implementation fg.deobf("curse.maven:modular-routers-250294:3776175")
    implementation fg.deobf("curse.maven:refined-storage-243076:4217424")
    implementation fg.deobf("curse.maven:quark-243121:3840125")
    implementation fg.deobf("curse.maven:applied-energistics-2-223794:4548560")
    implementation fg.deobf("curse.maven:simple-storage-network-268495:3868680")
    implementation fg.deobf("curse.maven:architects-palette-433862:4498424")
    implementation fg.deobf("curse.maven:autoreglib-250363:3642382")
    implementation fg.deobf("curse.maven:sophisticated-backpacks-vault-hunters-edition-696425:4287544")
    implementation fg.deobf("curse.maven:configured-457570:4462832")
    implementation fg.deobf("curse.maven:geckolib-388172:4181370")
    implementation fg.deobf("curse.maven:citadel-331936:3783096")
    implementation fg.deobf("curse.maven:jei-238222:4593548")
    implementation fg.deobf("curse.maven:sophisticated-core-618298:4665730")
    implementation fg.deobf("curse.maven:sophisticated-backpacks-422301:4637292")
    implementation fg.deobf("curse.maven:storage-drawers-223852:3807626")
    implementation fg.deobf("curse.maven:toms-storage-378609:5211203")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.6"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.3.6")) {
        jarJar.ranged(it, "[0.3.6,)")
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "vaultfilters",
                "Specification-Vendor"    : "vaultfilterssareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "vaultfilterssareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass" : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
